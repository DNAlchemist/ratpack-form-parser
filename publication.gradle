apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

def hasCredentials = hasProperty('ossrhUsername') && hasProperty('ossrhPassword')

uploadArchives.doFirst {
    if (!hasCredentials) {
        throw new InvalidUserDataException('Missing properties "ossrhUsername" and "ossrhPassword"')
    }
}

if (hasCredentials) {
    configureDeployment()
}

@SuppressWarnings(["GroovyAssignabilityCheck", "GrUnresolvedAccess"])
def configureDeployment() {

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    if (project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile')) {
                        signing.signPom(it)
                    } else {
                        logger.warn("Unable to generate signature: no signatory is available to sign")
                    }
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Ratpack form parser'
                    description 'Ratpack extension for parsing requests'
                    packaging 'jar'
                    url 'https://ratpack.chest.one'

                    scm {
                        url 'https://github.com/DNAlchemist/ratpack-form-parser'
                        connection 'scm:git:git://github.com/DNAlchemist/ratpack-form-parser.git'
                        developerConnection 'scm:git:git://github.com/DNAlchemist/ratpack-form-parser.git'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://www.opensource.org/licenses/mit-license.php'
                        }
                    }

                    developers {
                        developer {
                            id 'dnalchemist'
                            name 'Mikhalev Ruslan'
                            email 'mikhalev.ruslan@gmail.com'
                        }
                    }
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }
        }
    }

}