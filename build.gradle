group = 'one.chest.ratpack'
version = '0.0.3'

apply plugin: 'groovy'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.10.0'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1'
    }
}

apply from: 'coverage.gradle'
apply from: 'license.gradle'
apply from: 'publication.gradle'

repositories {
    jcenter()
}

dependencies {
    compileOnly 'io.ratpack:ratpack-groovy:1.4.6'

    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    compile 'javax.validation:validation-api:1.1.0.Final'

    testCompile 'io.ratpack:ratpack-groovy:1.4.6'

    testCompile 'org.glassfish.web:el-impl:2.2'
    testCompile 'org.hibernate:hibernate-validator:5.4.1.Final'

    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filesMatching('**/org.codehaus.groovy.runtime.ExtensionModule') {
        filter(ReplaceTokens, tokens: [NAME: project.name, VERSION: project.version])
    }
}